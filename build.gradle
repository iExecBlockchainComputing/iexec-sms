plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.10.2'
    id 'org.springframework.boot' version '3.3.8'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'maven-publish'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

allprojects {
    group = 'com.iexec.sms'
    if (gitBranch != 'main' && gitBranch != 'master' && !(gitBranch ==~ '(release|hotfix|support)/.*')) {
        version += '-NEXT-SNAPSHOT'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://docker-regis-adm.iex.ec/repository/maven-public/'
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven {
            url 'https://jitpack.io'
        }
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

dependencies {
    // iexec
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
    implementation project(':iexec-sms-library')

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry'

    // H2
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2:2.2.224'

    // Spring Doc
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // ssl
    implementation 'org.apache.httpcomponents.client5:httpclient5'

    // observability
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // expiring map
    implementation "net.jodah:expiringmap:0.5.11"
}

springBoot {
    buildInfo()
}

tasks.named("bootJar") {
    manifest {
        attributes("Implementation-Title": "iExec Secret Management Service",
                "Implementation-Version": project.version)
    }
}

testing {
    suites {
        configureEach {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
                runtimeOnly 'com.h2database:h2:2.2.222'
            }
        }
        test {
            useJUnitJupiter()
            dependencies {
                implementation 'org.awaitility:awaitility'
            }
        }
        itest(JvmTestSuite) {
            sources {
                java {
                    srcDirs = ['src/itest/java']
                }
            }
            dependencies {
                implementation project()
                implementation project(':iexec-sms-library')
                implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
                implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
                implementation 'org.apache.commons:commons-lang3'
                implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
                implementation 'org.springframework.boot:spring-boot-starter-web'
            }
        }
    }
}

tasks.withType(Test).configureEach {
    finalizedBy tasks.jacocoTestReport
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            artifact tasks.named("bootJar")
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url project.hasProperty('nexusUrl') ? nexusUrl : ''
        }
    }
}

ext.jarPathForOCI = relativePath(tasks.bootJar.outputs.files.singleFile)
ext.gitShortCommit = 'git rev-parse --short=8 HEAD'.execute().text.trim()
ext.ociImageName = 'local/' + ['bash', '-c', 'basename $(git config --get remote.origin.url) .git'].execute().text.trim()

tasks.register('buildImage', Exec) {
    group 'Build'
    description 'Builds an OCI image from a Dockerfile.'
    dependsOn bootJar
    commandLine 'docker', 'build', '--build-arg', 'jar=' + jarPathForOCI, '-t', ociImageName + ':dev', '.'
}

tasks.register('buildSconeImage', Exec) {
    group "Build"
    description "Build an OCI image compatible with scontain TEE framework"
    dependsOn buildImage
    commandLine "docker/sconify.sh"
    environment "IMG_FROM", "$ociImageName:dev"
    environment "IMG_TO", "$ociImageName-unlocked:dev"
}
