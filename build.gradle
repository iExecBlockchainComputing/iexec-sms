plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.2.2'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
    id 'maven-publish'
}

ext {
    openFeignVersion = '11.10'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

allprojects {
    group = 'com.iexec.sms'
    if (gitBranch != 'main' && gitBranch != 'master' && !(gitBranch ==~ '(release|hotfix|support)/.*')) {
        version += '-NEXT-SNAPSHOT'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://docker-regis-adm.iex.ec/repository/maven-public/'
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven {
            url 'https://jitpack.io'
        }
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
        }
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            srcDir 'src/itest/java'
        }
        resources.srcDir 'src/itest/resources'
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    // iexec
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
    implementation project(':iexec-sms-library')

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry'
    // H2
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2:1.4.200'

    // Spring Doc
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.3'

    //ssl
    implementation 'org.apache.httpcomponents:httpclient'

    // observability
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // feign
    implementation "io.github.openfeign:feign-jackson:$openFeignVersion"
    implementation "io.github.openfeign:feign-slf4j:$openFeignVersion"

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // awaitility
    testImplementation 'org.awaitility:awaitility'
}

springBoot {
    buildInfo()
}

tasks.named("bootJar") {
    manifest {
        attributes("Implementation-Title": "iExec Secret Management Service",
                   "Implementation-Version": project.version)
    }
}

test {
    useJUnitPlatform()
}

tasks.register('itest', Test) {
    group       'Verification'
    description 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false } // run always
    useJUnitPlatform()
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.test.finalizedBy    tasks.jacocoTestReport
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            artifact tasks.named("bootJar")
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url project.hasProperty('nexusUrl') ? nexusUrl : ''
        }
    }
}

ext.jarPathForOCI  = relativePath(tasks.bootJar.outputs.files.singleFile)
ext.gitShortCommit = 'git rev-parse --short=8 HEAD'.execute().text.trim()
ext.ociImageName   = 'local/' + ['bash', '-c', 'basename $(git config --get remote.origin.url) .git'].execute().text.trim()

tasks.register('buildImage', Exec) {
    group       'Build'
    description 'Builds an OCI image from a Dockerfile.'
    dependsOn   bootJar
    commandLine ('sh', '-c', "docker build --build-arg jar=$jarPathForOCI"
            + " -t $ociImageName:$gitShortCommit . && docker tag $ociImageName:$gitShortCommit $ociImageName:dev")
    standardOutput = new ByteArrayOutputStream()

    ext.output = {
        println standardOutput
        return standardOutput.toString()
    }
}

tasks.register('buildSconeImage', Exec) {
    group       "Build"
    description "Build an OCI image compatible with scontain TEE framework"
    dependsOn   buildImage
    commandLine "docker/sconify.sh"
    environment "IMG_FROM", "$ociImageName:dev"
    environment "IMG_TO", "$ociImageName-unlocked:dev"
}
